# Interactive tests for jobs, bg, fg and related builtins
set pid [spawn $ksh]
expect_prompt

# ======
# Ctrl-Z should stop current process
log_test_entry
send "cat\r"
sleep 0.5
send [ctrl Z]
expect -re ".*Stopped.*cat\r\n" {
    puts "Ctrl-z stops cat processs"
}
expect_prompt

# ======
# Test if jobs builtin lists stopped process
log_test_entry
send "jobs\r"
expect -re ".*Stopped.*cat\r\n" {
    puts "jobs lists stopped cat process"
}
expect_prompt

# ======
# Test if jobs builtin lists pid of stopped process
log_test_entry
send "jobs -l\r"
expect -re "\[1\].*\[0-9\]+.*Stopped.*cat\r\n" {
    puts "jobs -l lists pid of stopped cat process"
}
expect_prompt

# ======
# Test if jobs builtin lists process groups of stopped process
log_test_entry
send "jobs -p\r"
expect -re "\[0-9\]+\r\n" {
    puts "jobs -p lists process group of stopped cat process"
}
expect_prompt

# ======
# bg should put last stopped process in background
log_test_entry
send "bg\r"
expect -re ".*cat&\r\n"
expect_prompt

send "jobs\r"
expect -re ".*Stopped.*SIGTTIN.*cat\r\n" {
    puts "bg brings last stopped process to background"
}
expect_prompt

# ======
# fg should put last stopped process in foreground
log_test_entry
send "fg\r"
sleep 0.1
send "hello world\r\n"
send [ctrl D]
expect -re "\r\nhello world\r\n" {
    puts "fg brings last stopped process to foreground"
}
expect_prompt

# ======
# There should be a warning if shell attempts exit with stopped processes.
# The signal delivery timing (especially on platforms like Windows Subsystem for Linux, WSL) might
# result in the next prompt being issued before the "Stopped" message and a second prompt being
# displayed. So we might need to increment the prompt counter.
log_test_entry
send "cat\r"
sleep 0.5
send [ctrl Z]
expect -re ".*Stopped.*cat\r\n"
if {[string match "\r\nKSH PROMPT:*" $expect_out(buffer)]} {
    incr prompt_counter
}
expect_prompt

# Try to exit shell
send [ctrl D]
expect -re ".*You have stopped jobs\r\n" {
    puts "Warning before exit for stopped jobs works"
}
expect_prompt

# This should actually exit the shell
send [ctrl D]
catch {expect default exp_continue} output
log_debug "EOF output: $output"

catch {wait}
exit 0
